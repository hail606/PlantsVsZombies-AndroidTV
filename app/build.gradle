plugins {
    id "com.android.application"
}

def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file("keystore.properties")
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    Runtime.getRuntime().exec(
            "keytool", "-genkeypair",
            "-v", "-keystore", "${System.getProperty("user.home")}/debug.keystore",
            "-storepass", "android",
            "-keypass", "android",
            "-alias", "androiddebugkey",
            "-keyalg", "RSA",
            "-keysize", "2048",
            "-validity", "10000",
            "-dname", "CN=Android Debug,O=Android,C=US"
    ).waitFor()
}

android {
    namespace = "com.trans.pvztv"
    ndkVersion = "27.2.12479018"
    compileSdk = 34

    defaultConfig {
        applicationId = "com.trans.pvztv"
        minSdk = 21
        targetSdk = 33
        versionCode = 2
        versionName = "0.0.0"
        versionNameSuffix = '-' + new Date().format("yyMMdd")

        ndk {
            // 修改这一行，添加 arm64-v8a
             abiFilters "arm64-v8a"
        }
    }

    signingConfigs {
        // 本文件已被 git 跟踪, 不要在此处修改签名配置, 需要自定义签名配置请参考 README.md
        release {
            keyAlias = keystoreProperties.getProperty("keyAlias", "androiddebugkey")
            keyPassword = keystoreProperties.getProperty("keyPassword", "android")
            storeFile = file(keystoreProperties.getProperty("storeFile", "${System.getProperty("user.home")}/debug.keystore"))
            storePassword = keystoreProperties.getProperty("storePassword", "android")
        }
    }

    buildTypes {
        debug {
            applicationIdSuffix = ".debug"
        }

        release {
            signingConfig = signingConfigs.release
            externalNativeBuild {
                cmake {
                    arguments "-DCMAKE_BUILD_TYPE=Release" // will be "RelWithDebInfo" if not set
                }
            }
            /* Uncommenting will cause a crash. */
            // minifyEnabled = true
            // shrinkResources = true
        }
    }

    flavorDimensions += "version"

    productFlavors {
        register("v111") {
            dimension = "version"
            applicationId = "com.trans.pvz"
            versionName = "1.1.1"
            externalNativeBuild {
                cmake {
                    arguments "-DPVZ_VERSION=111"
                }
            }
        }

        register("v115") {
            dimension = "version"
            versionName = "1.1.5"
            externalNativeBuild {
                cmake {
                    arguments "-DPVZ_VERSION=115"
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }

    externalNativeBuild {
        cmake {
            path = "src/main/cpp/CMakeLists.txt"
        }
    }

    lintOptions {
        checkReleaseBuilds = false
        abortOnError = false
    }

    applicationVariants.configureEach { variant ->
        variant.outputs.configureEach { output ->
            outputFileName = "PvZ-TV-v${variant.versionName}-${variant.buildType.name}.apk"
        }
    }
}

dependencies {
    implementation fileTree("dir": "libs", "include": ["*.jar"])
}
